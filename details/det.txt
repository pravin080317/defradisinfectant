// Get login events
customEvents
| where name == "LAPortalLogin"
| where timestamp between (datetime(2025-07-01T00:00:00Z) .. now())
| extend 
    userId = tostring(customDimensions.userId),
    userName = tostring(customDimensions.userName),
    userCouncil = tostring(customDimensions.userCouncil),
    sessionId = tostring(customDimensions.sessionId),
    loginTime = todatetime(customDimensions.loginTime),
    isMultiCouncil = tostring(customDimensions.isUserRepresentsMultipleCouncil)
| where userCouncil != "Unknown" and isnotempty(userCouncil)
| project timestamp, eventType="Login", userId, userName, sessionId, userCouncil, loginTime, isMultiCouncil

// Combine with page views
| union (
    pageViews
    | where timestamp between (datetime(2025-07-01T00:00:00Z) .. now())
    | extend 
        userId = tostring(customDimensions.userId),
        userName = tostring(customDimensions.userName),
        sessionId = tostring(customDimensions.sessionId),
        userCouncil = tostring(customDimensions.userCouncil)
    | where userCouncil != "Unknown" and isnotempty(userCouncil)
    | project timestamp, eventType="PageView", userId, userName, sessionId, userCouncil, loginTime=datetime(null), isMultiCouncil=""
)

// Summarize per session
| summarize 
    councilsVisited = make_set(userCouncil),
    totalCouncils = dcount(userCouncil),
    userName = any(userName),
    firstLogin = minif(timestamp, eventType == "Login"),
    pagesVisited = countif(eventType == "PageView")
by userId, sessionId
| where totalCouncils > 1
| order by firstLogin desc
